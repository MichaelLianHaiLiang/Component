if(rootProject.ext.IS_APP){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android['compileSdkVersion']
    defaultConfig {

        multiDexEnabled true
        if(rootProject.ext.IS_APP){
            applicationId "com.hl.myapp"
        }

        minSdkVersion rootProject.ext.android['minSdkVersion']
        targetSdkVersion rootProject.ext.android['targetSdkVersion']
        versionCode rootProject.ext.android['versionCode']
        versionName rootProject.ext.android['versionName']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("moduleName", project.getName())
            }
        }
    }

    //签名配置信息
    signingConfigs {
        if(rootProject.ext.IS_APP){
            debug {
                storeFile file(APP_JKS_DEBUG_PATH)
            }
            release {
                storeFile file(APP_JKS_PATH)
                storePassword APP_JKS_STORE_PASSWORD
                keyAlias APP_JKS_ALIAS
                keyPassword APP_JKS_PASSWORD
            }
        }
    }

    //签名
    buildTypes {
        if(rootProject.ext.IS_APP){//application
            debug{
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

                buildConfigField('String', 'IS_NEED_APP_SUBJECT', '"'+rootProject.ext.IS_NEED_APP_SUBJECT+'"')
                buildConfigField('String', 'IS_NEED_APP_SITE', '"' + rootProject.ext.IS_NEED_APP_SITE + '"')
                buildConfigField('String', 'IS_NEED_APP_IM', '"' + rootProject.ext.IS_NEED_APP_IM + '"')
                buildConfigField('String', 'IS_NEED_APP_CANCEL', '"' + rootProject.ext.IS_NEED_APP_CANCEL + '"')
            }

            release {
                debuggable false
                jniDebuggable false
                signingConfig signingConfigs.release
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

                buildConfigField('String', 'IS_NEED_APP_SUBJECT', '"'+rootProject.ext.IS_NEED_APP_SUBJECT+'"')
                buildConfigField('String', 'IS_NEED_APP_SITE', '"' + rootProject.ext.IS_NEED_APP_SITE + '"')
                buildConfigField('String', 'IS_NEED_APP_IM', '"' + rootProject.ext.IS_NEED_APP_IM + '"')
                buildConfigField('String', 'IS_NEED_APP_CANCEL', '"' + rootProject.ext.IS_NEED_APP_CANCEL + '"')

            }
        }else {//library
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    //指定清单文件
    sourceSets {
        main {
            if (rootProject.ext.IS_APP) {
                manifest.srcFile 'src/release/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/debug/AndroidManifest.xml'
            }
        }
    }

    if (rootProject.ext.IS_APP){
        applicationVariants.all {
            def buildType = it.buildType.name
            def versionName = it.versionName
            def versionCode = it.versionCode
            def fileName
            it.outputs.each {
                fileName = "app_${buildType}_${versionName}_${versionCode}.apk"
                it.outputFileName = fileName
            }
        }
    }

    dexOptions {
        //最大堆内存
        javaMaxHeapSize "8g"
        //是否支持大工程模式
        jumboMode = true
        //预编译
        preDexLibraries = true
        //线程数
        threadCount = 8
    }

    lintOptions {
        abortOnError false
    }

    //此处设置为true才会引入databinding
    dataBinding {
        enabled = true
    }

    kapt {
        generateStubs = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation rootProject.ext.dependencies['test']
    androidTestImplementation rootProject.ext.dependencies['androidTest']

    //主工程app作为独立工程提供 需要基础工程作为组件提供 判断需要哪些组件
    //当基础工程作为独立工程时需要使用公用登录模块就直接依赖此模块使用,不需要时就不依赖,自己独自写一套登录模块
    if (rootProject.ext.IS_NEED_APP_SUBJECT) {
        implementation project(':appSubject')
    }

    if (rootProject.ext.IS_NEED_APP_SITE) {
        implementation project(':appSite')
    }

    if (rootProject.ext.IS_NEED_APP_IM) {
        implementation project(':appIM')
    }

    if (rootProject.ext.IS_NEED_APP_CANCEL) {
        implementation project(':appCancel')
    }

    implementation project(':componentLogin')
    implementation project(':coreLib')

}
